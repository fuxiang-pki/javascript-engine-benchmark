plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

def os = System.getProperty('os.name').toLowerCase().split()[0];
println "os: ${os}"

def j2v8ArtifactId

if (os == 'windows') {
    j2v8ArtifactId = 'j2v8_win32_x86_64'
} else if (os == 'linux') {
    j2v8ArtifactId = 'j2v8_linux_x86_64'
} else {
    j2v8ArtifactId = 'j2v8_macosx_x86_64'
}

dependencies {
    // Nashorn script engine
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'

    // GraalJS polyglot context
    implementation 'org.graalvm.js:js-scriptengine:22.3.1'

    // GraalJS script engine
    implementation 'org.graalvm.js:js:22.3.1'

    // J2V8
    implementation "com.eclipsesource.j2v8:${j2v8ArtifactId}:4.6.0"
}

tasks.register('runWithGraalJIT', JavaExec) {
    mainClass.set('org.example.App')
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs(
            "-XX:+UnlockExperimentalVMOptions",
            "-XX:+EnableJVMCI",
            "-XX:+UseJVMCICompiler",
            "--upgrade-module-path=libs/compiler-22.3.1.jar;libs/compiler-management-22.3.1.jar",
            "--module-path=libs/graal-sdk-22.3.1.jar;libs/truffle-api-22.3.1.jar"
    )
}

tasks.register('run', JavaExec) {
    mainClass.set('org.example.App')
    classpath = sourceSets.main.runtimeClasspath
}